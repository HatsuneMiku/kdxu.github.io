class: center, middle

### Agdaによる正当性が証明された
### 型推論器の改良と拡張

=========

2015.12.11

浅井研 M2 門脇香子

---

# この研究は

Agdaによる正当性が保証された型推論器を実装している

---

## キーワード

+ Agda
+ 依存型
+ 型推論
+ W-algorithm

---

## Agda

- 定理証明支援系　兼　プログラミング言語
- 型が命題, 実装が証明
- 文法は Haskell に似ている
- 依存型 を使うことができる

## 依存型


- 値に依存する型を作ることのできる型のこと
- たとえば `Fin n` 型は `n` をパラメーターとして持つ、 `0` から `n-1` までの finite set として表現
- 時間がないので省略します

---

# 研究背景

- その昔、Agdaによる型チェッカーはあった
- ただし、束縛変数の型があらかじめ与えられていることを仮定していた（つまり，型チェッカーではあるが型推論器ではな
  い）
-  一般に束縛変数の型を含めて型を推論しようとすると、型の unification が必要
- 型推論器は書き換え可能なセルを使うと簡単に実装することができるけど、 Agda だと書き換えができない
- Agda で書けば停止性と正当性が示された型推論器がエレガントに実装できるのでは、という話

---

## 停止性

- プログラムが有限の計算回数で止まることが保証されていること

---

## 正当性

![infer](./images/infer.png)

- 型推論前と型推論後の syntax で確かに意味が変わらないということ

---

# 実装と改良

- 停止性、正当性が示された型推論器は一旦実装できた（卒論, PPL2015, prosym15 で発表)
- この型推論器には問題があった
- 型の変数の数をいちいち`count`関数で数えて、手計算で足したり引いたりしてやらないといけなくて面倒だった
- また、このままだと `fst`, `snd`, `cons` などが実装できなかった

cons operator
```ocaml
[1; 2; 3]
1 :: [2; 3]
1 :: 2 :: [3]
1 :: 2 :: 3 :: []
```

---

# 改良したこと

- 型定義を改良
- 型変数の数ではなく、型変数に関する不等式を持ち歩くようにした


---

# 今回の流れ

- unification の実装
- (新しい) 構文定義、型定義
- (新しい) `infer` 関数の実装
- できたところ、できていないところ

---

# unification に関して

- unification に関しては、これまで通り McBride の手法を採用

- 型の unification は，書き換え可能なセルを使うと簡明に実装できる

- しかし、Agda では再代入は許されないのに加えて、停止性が保証されている必要がある

- McBride は「型変数が具体化されるたびに、具体化されていない型変数の数がひとつ減る」という点に注目

- 具体的には、 `n` 個の「具体化されていない型変数」を `Fin n` 型の数字で表現し，その `n` を減らすことで停止性が明らかな形の unification を実現している

---

![unify](./images/unify.png)

---

## syntax と型定義

### well-scoped term

- well-scoped term は、型レベルで `n` というパラメータを持つ term

- この`n` は、 term の中の自由変数の数

#### definition

```agda
data WellScopedTerm (n : ℕ) : Set where
  Var : Fin n → WellScopedTerm n
  Lam : (s : WellScopedTerm (suc n)) → WellScopedTerm n
  App : (s1 : WellScopedTerm n)
      → (s2 : WellScopedTerm n) → WellScopedTerm n
  Fst : WellScopedTerm n → WellScopedTerm n
  Snd : WellScopedTerm n → WellScopedTerm n
  Cons : WellScopedTerm n → WellScopedTerm n → WellScopedTerm n

```

 - `Fin n` 型は 0~n-1の自然数の有限集合をもつ型のこと

差分 : `Fst`, `Snd`, `Cons` が増えた

---

### well-typed term

型環境`Γ`と型`t`を（また型レベルの）パラメータとして持ち歩く term

#### definition

```agda
-- WellTypedTerm Γ t : 自由変数の数が n 個、型が t であるような well-typed な項
data WellTypedTerm {m n : ℕ} (Γ : Cxt n) : Type m → Set where
  Var : (x : Fin n) → WellTypedTerm Γ (lookup x Γ)
  Lam : (t : Type m) → {t' : Type m}
      → WellTypedTerm (t ∷ Γ) t' → WellTypedTerm Γ (t ⇒ t')
  App : {t t' : Type m} → WellTypedTerm Γ (t ⇒ t')
        → WellTypedTerm Γ t → WellTypedTerm Γ t'
  Fst : {t1 t2 : Type m}
        → WellTypedTerm Γ (t1 ∏ t2) →  WellTypedTerm Γ t1
  Snd : {t1 t2 : Type m}
        → WellTypedTerm Γ (t1 ∏ t2) →  WellTypedTerm Γ t2
  Cons :  {t1 t2 : Type m} → WellTypedTerm Γ t1
        → WellTypedTerm Γ t2 → WellTypedTerm Γ (t1 ∏ t2)
```
差分 : `Fst`, `Snd`, `Cons` が増えた

---

### 型定義

- `Type` を定義するにあたり `Desc` 型 と `Fix` 型を用いた(new!)
- `Desc` は型の再帰やコンストラクタの記述を抽象化したもの

#### definition
```agda
data Desc : Set₁ where
  base  : Desc
  _:+:_ : Desc → Desc → Desc
  _:*:_ : Desc → Desc → Desc
  rec   : Desc

-- 最大で m 個のメタ変数を持つ型を表す型
data Fix (D : Desc) (m : ℕ) : Set where
  F : ⟦ D ⟧ (Fix D m) → Fix D m
  M : (x : Fin m) → Fix D m
```

---

たとえば、
```ocaml
t = TNat | t ⇒ t | TVar x
```

みたいな型は

```agda
TypeDesc : Desc
TypeDesc = base :+: rec :*: rec :+: rec :*: rec

Type : (m : ℕ) → Set
Type m = Fix TypeDesc m
```
と書け、

`TNat` : `base` (基底型)

`:*:` 連結

`t ⇒ t`　<=> `rec :*: rec`

`:*:` 連結

`TVar x`   <=> `rec :*: rec` のような形式で記述できる

---

#### こうすることのメリット

- `TypeDesc` によってよしなに型定義が書ける

以前は、　

```agda
data Type : Set where
  TNat : Type
  TVar x : Type -> Type
  _=>_ : Type -> Type -> Type
```
のようにコンストラクタ単位で型を書いていたので、ここを変えると全部変えないといけなくて大変

これからはそんな心配をしなくても済むように...


---

### 代入の型

- `AList D m n` : `m` 個の型変数を持つ型を `n` 個の型変数を持つ型にする代入

```agda
data AList (D : Desc) : ℕ → ℕ → Set where
  anil : {m : ℕ} → AList D m m -- 何もしない代入
  _asnoc_/_ : {m : ℕ} {m' : ℕ} → (σ : AList D m m') → (t' : Fix D m) →
              (x : Fin (suc m)) → AList D (suc m) m'

AListType : ℕ → ℕ → Set
AListType m m' = AList TypeDesc m m'
```

---

# 型推論器の実装

- 型推論のアルゴリズムとして W-algorithm を採用

#### 新しい関数定義

```agda
infer : (m : ℕ) → {n : ℕ}
          → (Γ : Cxt {m} n)
          → (s : WellScopedTerm n) →
         Maybe (Σ[ m'' ∈ ℕ ]
                Σ[ m' ∈ ℕ ]
                Σ[ m≤m'' ∈ m ≤ m'' ]
                Σ[ σ ∈ AListType m'' m' ]
                Σ[ τ ∈ Type m' ]
                WellTypedTerm (substCxt≤ σ m≤m'' Γ) τ)
```

---

```agda
infer : (m : ℕ) → {n : ℕ}
          → (Γ : Cxt {m} n)
          → (s : WellScopedTerm n) →
         Maybe (Σ[ m'' ∈ ℕ ]
                Σ[ m' ∈ ℕ ]
                Σ[ m≤m'' ∈ m ≤ m'' ]
                Σ[ σ ∈ AListType m'' m' ]
                Σ[ τ ∈ Type m' ]
                WellTypedTerm (substCxt≤ σ m≤m'' Γ) τ)
```

- `m` 個の型変数をもつ `WellScopedTerm s` を渡すと、
- ある `m'` と `m''` (ここで`m ≤ m''`) と
- 型変数の数を `m''` から `m'` にする代入 `σ` と
- 型`τ`と
- `WellTypedTerm` が返ってくる


---

#### なぜ不等式を導入したか

- 以前は、型変数の数をきっちり管理していた

- 手計算するのが大変だったし、よしなにやってほしい。`Fst`,`Snd` が増やせない(彼らの型変数の数は正確に計算できない!)

- 型推論するにあたって正確な型変数の数は常に必要ではないのでは？

- `σ : AListType m'' m'`は `m''` 個の型変数を持つ型を `m'` 個の型変数を持つ型にする代入

- 制約は `m ≤ m''` で十分

---

### 実装本体

#### Variable

変数の場合は単に型環境の中の型を返す

```agda
infer m Γ (Var x) =
      just (m , (m , ((n≤m+n 0 m) , (anil , ((lookup x Γ) , VarX)))))
        where
          VarX : WellTypedTerm (substCxt≤ anil (n≤m+n 0 m) Γ) (lookup x Γ)
          VarX rewrite substCxt≤Anil Γ (n≤m+n 0 m) = Var x
```

---

### 実装本体

#### Lambda

- Lambda の場合はまず body 部分の型推論を行う
- body 部分には　`m` 番目の新たな変数が型環境に加わる
- e.g `TVar (fromℕ m) ∷ liftCxt 1 Γ)`
- body 部分に型が付いたら「引数の型⇒body の型」が全体の型
- 引数の型変数は body を型推論中に具体化されているかも知れないので
代入を施している

---

```agda
infer m Γ (Lam s) with infer (suc m) (TVar (fromℕ m) ∷ liftCxt 1 Γ)
         s
... | just  (m'' , m' , leq , σ , t , w) =
  just (m'' , (m' , (leq' , (σ , (tx ⇒ t , LamS)))))
  where
    leq' : m ≤ m''
    leq' = DecTotalOrder.trans decTotalOrder (n≤m+n 1 m) leq

    tx : Type m'
    tx = substType≤ σ leq (TVar (fromℕ m))

    LamS : WellTypedTerm (substCxt≤ σ leq' Γ) (tx ⇒ t)
    LamS = Lam (mvar-sub σ (inject≤ (fromℕ m) leq)) w'
     where
        w' : WellTypedTerm (tx ∷ substCxt≤ σ leq' Γ) t
        w' = subst (λ l → WellTypedTerm (tx ∷ l) t) eq w
           where eq : substCxt≤ σ leq (liftCxt 1 Γ) ≡ substCxt≤ σ leq' Γ
                 eq = substCxt≤+1 Γ leq leq' σ

infer m Γ (Lam s) | nothing = nothing
```

---

### 実装本体

#### Application
- `App s1 s2` の型推論はまず `s1` と `s2` の型推論を行い，
次に `s1` の型が「`s2 の型⇒β`」の形になっていることを
確認する(unification)
- ここで正しく unify することが示された `unify` 関数が必要になるので、今それを書こうとしてる
- この `unify` 関数は`Fst`, `Snd` , `Cons` でも必要になる

---

進捗状況
----
- まだ証明を書き終えていない


まとめと今後の課題
----

- 本研究では，Agdaを用いて Simple-Typed Lambda Calculus の unification
の機構を McBride の手法により克服し， 型推論器の実装と定式化を行い、不等式や`TypeDesc`を用いてその改善に取り組んでいる

- 終わっていない証明を終わらせたい (`Fst`, `Snd`, `Cons`, `App`)

- コードと今回の資料 : https://github.com/kdxu/InferAgda
